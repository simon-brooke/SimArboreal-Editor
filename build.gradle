apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'application'

version='1.0.1-SNAPSHOT'
group='com.simsilica'

mainClassName = 'com.simsilica.arboreal.TreeEditor'

applicationDefaultJvmArgs = ["-Xmx512m", "-XX:MaxDirectMemorySize=512m"]

repositories {
    mavenLocal()
    jcenter()
    
    // Temporary until JME jars are in jcenter()   
    //maven { url "http://dl.bintray.com/jmonkeyengine/org.jmonkeyengine" }
    
    // Meta-jb stuff        
    maven { url "https://svn.code.sf.net/p/meta-jb/svn/trunk/dev/m2-repo/" }        
}

// Make sure the build file declares what it actually imports
configurations.implementation {
    transitive = false
}

sourceSets.main.resources { 
    srcDirs += "src/main/java" 
    exclude "**/*.java" 
    exclude "**/*.tmp" 
}

// In this section you declare the dependencies for your production and test code
dependencies {

    implementation "org.jmonkeyengine:jme3-core:3.1.+"
    implementation "org.jmonkeyengine:jme3-desktop:3.1.+" 
    implementation "org.jmonkeyengine:jme3-effects:3.1.+" 
    implementation "org.jmonkeyengine:jme3-lwjgl:3.1.+" 
     
    implementation "com.simsilica:sim-arboreal:1.0.1-SNAPSHOT"
    runtimeOnly 'com.simsilica:sim-arboreal:1.0.1-SNAPSHOT:assets'

    implementation "com.simsilica:lemur:1.6.+"
    implementation "com.simsilica:lemur-proto:1.5.+"
    implementation "com.simsilica:lemur-props:1.0.+"
    runtimeOnly 'org.codehaus.groovy:groovy-all:2.4.5'

    implementation "com.simsilica:pager:1.0.+"
    implementation "com.simsilica:sim-fx:1.0.+"
    // It does not seem that SimFX has or should have an assets jar?
    // runtimeOnly "com.simsilica:sim-fx:1.0.+:assets"

    implementation 'org.meta-jb:meta-jb-json:1.0.1'

    implementation 'org.slf4j:slf4j-api:1.7.13'
        
    runtimeOnly files("assets")        
}


// Configuration to produce maven-repo style -sources and -javadoc jars
task sourcesJar(type: Jar) {
    duplicatesStrategy = 'WARN'
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    exclude '**/.backups' 
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}
 
task assetsJar(type: Jar) {
    archiveClassifier = 'assets'
    from file('assets')
    exclude '**/*.psd' 
    exclude '**/.backups' 
}
 
artifacts {
    archives sourcesJar
    archives javadocJar
    archives assetsJar
}

task uberjar(type: Jar) {
    from(configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    
    archiveBaseName = 'sim-arboreal-editor-UBERJAR'
    duplicatesStrategy = DuplicatesStrategy.WARN

    manifest {
        attributes 'Implementation-Title': 'SimArboreal-Editor',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Main-Class': mainClassName
    }
}

